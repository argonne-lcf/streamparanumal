/*

  The MIT License (MIT)

  Copyright (c) 2017-2022 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:

  The above copyright notice and this permission notice shall be included in all
  copies or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  SOFTWARE.

*/

@kernel void bs5_1(const dlong Nblocks,
                   const dlong N,
                   @restrict const dfloat *Ap,
                   const dfloat alpha,
                   @restrict       dfloat *r,
                   @restrict       dfloat *rdotr){

for(dlong b=0;b<Nblocks;++b;@outer(0)){

    @shared dfloat s_ddot[p_blockSize];

    for(int t=0;t<p_blockSize;++t;@inner(0)){
      dlong id = t + b*p_blockSize;
      dfloat r_ddot = 0.0;
      while (id<N) {
        const dfloat r_r = r[id] + alpha*Ap[id];
        r[id] = r_r;
        r_ddot += r_r*r_r;
        id += p_blockSize*Nblocks;
      }
      s_ddot[t] = r_ddot;
    }

#if p_blockSize>512
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<512) s_ddot[t] += s_ddot[t+512];
#endif

#if p_blockSize>256
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<256) s_ddot[t] += s_ddot[t+256];
#endif

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<128) s_ddot[t] += s_ddot[t+128];
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t< 64) s_ddot[t] += s_ddot[t+ 64];
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t< 32) s_ddot[t] += s_ddot[t+ 32];
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t< 16) s_ddot[t] += s_ddot[t+ 16];
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  8) s_ddot[t] += s_ddot[t+  8];
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  4) s_ddot[t] += s_ddot[t+  4];
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  2) s_ddot[t] += s_ddot[t+  2];
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  1) rdotr[b] = s_ddot[0] + s_ddot[1];
  }
}


@kernel void bs5_2(const dlong Nblocks,
                   @restrict const  dfloat *a,
                   @restrict        dfloat *ddot){


  for(dlong b=0;b<1;++b;@outer(0)){

    @shared dfloat s_ddot[p_blockSize];

    for(int t=0;t<p_blockSize;++t;@inner(0)){
      dlong id = t + b*p_blockSize;
      dfloat r_ddot = 0.0;
      while (id<Nblocks) {
        r_ddot += a[id];
        id += p_blockSize*Nblocks;
      }
      s_ddot[t] = r_ddot;
    }

#if p_blockSize>512
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<512) s_ddot[t] += s_ddot[t+512];
#endif

#if p_blockSize>256
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<256) s_ddot[t] += s_ddot[t+256];
#endif

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<128) s_ddot[t] += s_ddot[t+128];
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t< 64) s_ddot[t] += s_ddot[t+ 64];
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t< 32) s_ddot[t] += s_ddot[t+ 32];
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t< 16) s_ddot[t] += s_ddot[t+ 16];
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  8) s_ddot[t] += s_ddot[t+  8];
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  4) s_ddot[t] += s_ddot[t+  4];
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  2) s_ddot[t] += s_ddot[t+  2];
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  1) ddot[b] = s_ddot[0] + s_ddot[1];
  }
}


